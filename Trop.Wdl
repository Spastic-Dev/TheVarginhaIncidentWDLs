/////////////////////////////////////////////////////////////////
// Trop Actor
// Copyright © 1997 Perceptum Informática Ltda
// All Rights Reserved
/////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////
// Bitmaps
/////////////////////////////////////////////////////////////////

BMAP Trop0001, <Trop0g.pcx>,  35,   5,  51, 111;
BMAP Trop0002, <Trop0g.pcx>, 153,   5,  55, 111;
BMAP Trop0003, <Trop0g.pcx>, 275,   5,  51, 110;
BMAP Trop0101, <Trop0g.pcx>,  35, 125,  51, 111;
BMAP Trop0102, <Trop0g.pcx>, 152, 125,  57, 111;
BMAP Trop0103, <Trop0g.pcx>, 275, 125,  51, 110;
BMAP Trop0201, <Trop0g.pcx>,  35, 245,  51, 111;
BMAP Trop0202, <Trop0g.pcx>, 154, 245,  53, 110;
BMAP Trop0203, <Trop0g.pcx>, 275, 245,  51, 110;

BMAP Trop1001, <Trop1g.pcx>,  38,  10,  45, 106;
BMAP Trop1002, <Trop1g.pcx>, 157,  10,  47, 104;
BMAP Trop1003, <Trop1g.pcx>, 271,  10,  59, 106;
BMAP Trop1004, <Trop1g.pcx>, 391,  10,  59, 107;
BMAP Trop1005, <Trop1g.pcx>, 518,  10,  45, 107;
BMAP Trop1101, <Trop1g.pcx>,  38, 125,  45, 110;
BMAP Trop1102, <Trop1g.pcx>, 163, 125,  35, 110;
BMAP Trop1103, <Trop1g.pcx>, 278, 125,  45, 109;
BMAP Trop1104, <Trop1g.pcx>, 392, 125,  57, 109;
BMAP Trop1105, <Trop1g.pcx>, 519, 125,  43, 109;
BMAP Trop1201, <Trop1g.pcx>,  38, 250,  45, 106;
BMAP Trop1202, <Trop1g.pcx>, 162, 250,  37, 105;
BMAP Trop1203, <Trop1g.pcx>, 272, 250,  57, 104;
BMAP Trop1204, <Trop1g.pcx>, 391, 250,  59, 106;
BMAP Trop1205, <Trop1g.pcx>, 519, 250,  43, 107;
BMAP Trop1301, <Trop1g.pcx>,  38, 365,  45, 110;
BMAP Trop1302, <Trop1g.pcx>, 163, 365,  35, 111;
BMAP Trop1303, <Trop1g.pcx>, 278, 365,  45, 111;
BMAP Trop1304, <Trop1g.pcx>, 392, 365,  57, 110;
BMAP Trop1305, <Trop1g.pcx>, 518, 365,  45, 109;

BMAP Trop3001, <Trop3g.pcx>,  36,  11,  49, 106;
BMAP Trop3101, <Trop3g.pcx>, 158,  14,  45, 104;
BMAP Trop3201, <Trop3g.pcx>, 280,  16,  41, 103;
BMAP Trop3301, <Trop3g.pcx>,  43, 137,  35, 102;
BMAP Trop3401, <Trop3g.pcx>, 162, 136,  37, 103;
BMAP Trop3501, <Trop3g.pcx>, 282, 136,  37, 103;

BMAP Trop5001, <Trop5g.pcx>,  25,  12,  71, 106;
BMAP Trop5002, <Trop5g.pcx>, 155,  13,  51, 103;
BMAP Trop5003, <Trop5g.pcx>, 269,  13,  63, 104;

BMAP Trop7001, <Trop7g.pcx>,  58,   8,  85, 185;
BMAP Trop7101, <Trop7g.pcx>, 258,   8,  85, 185;
BMAP Trop7201, <Trop7g.pcx>, 448,   8, 105, 186;
BMAP Trop7301, <Trop7g.pcx>,  53, 208,  95, 187;
BMAP Trop7401, <Trop7g.pcx>, 226, 208, 149, 187;
BMAP Trop7501, <Trop7g.pcx>, 444, 208, 113, 187;
BMAP Trop7601, <Trop7g.pcx>,  51, 408,  99, 187;
BMAP Trop7701, <Trop7g.pcx>, 250, 408, 101, 186;
BMAP Trop7801, <Trop7g.pcx>, 456, 408,  89, 185;

BMAP Trop8001, <Trop8g.pcx>,  33,   9,  55, 106;
BMAP Trop8101, <Trop8g.pcx>, 141,  17,  79,  98;
BMAP Trop8201, <Trop8g.pcx>, 255,  27,  91,  86;
BMAP Trop8301, <Trop8g.pcx>,  10, 169, 101,  63;
BMAP Trop8401, <Trop8g.pcx>, 133, 177,  95,  57;
BMAP Trop8501, <Trop8g.pcx>, 256, 187,  89,  47;
BMAP Trop8601, <Trop8g.pcx>,  19, 317,  83,  36;
BMAP Trop8701, <Trop8g.pcx>, 136, 320,  89,  32;

BMAP Trop9001, <Trop8g.pcx>, 253, 318,  95,  28;

BMAP TropA001, <TropAg.pcx>,  36,  21,  49,  94;
BMAP TropA002, <TropAg.pcx>, 161,  21,  39,  95;
BMAP TropA003, <TropAg.pcx>, 276,  21,  49,  95;
BMAP TropA004, <TropAg.pcx>, 388,  21,  65,  94;
BMAP TropA005, <TropAg.pcx>, 516,  21,  49,  93;
BMAP TropA101, <TropAg.pcx>,  33, 144,  55,  92;
BMAP TropA102, <TropAg.pcx>, 157, 144,  47,  90;
BMAP TropA103, <TropAg.pcx>, 271, 144,  59,  92;
BMAP TropA104, <TropAg.pcx>, 388, 144,  65,  93;
BMAP TropA105, <TropAg.pcx>, 515, 144,  51,  93;
BMAP TropA201, <TropAg.pcx>,  36, 261,  49,  94;
BMAP TropA202, <TropAg.pcx>, 161, 261,  39,  94;
BMAP TropA203, <TropAg.pcx>, 276, 261,  49,  93;
BMAP TropA204, <TropAg.pcx>, 388, 261,  65,  93;
BMAP TropA205, <TropAg.pcx>, 516, 261,  49,  93;
BMAP TropA301, <TropAg.pcx>,  37, 384,  47,  92;
BMAP TropA302, <TropAg.pcx>, 162, 384,  37,  91;
BMAP TropA303, <TropAg.pcx>, 272, 384,  57,  90;
BMAP TropA304, <TropAg.pcx>, 389, 384,  63,  92;
BMAP TropA305, <TropAg.pcx>, 516, 384,  49,  93;

/////////////////////////////////////////////////////////////////
// Sounds
/////////////////////////////////////////////////////////////////
SOUND Trop01Snd, <trop01.wav>;	// "Ok Charlie Delta Tango..."
SOUND Trop02Snd, <trop02.wav>;	// "Over there..."
SOUND Trop03Snd, <trop03.wav>;	// "Get down!"
SOUND Trop04Snd, <trop04.wav>;	// MP5 firirng
SOUND Trop05Snd, <trop05.wav>;	// "Uh!"
SOUND Trop06Snd, <trop06.wav>;	// "Help me over here!"
SOUND Trop07Snd, <trop07.wav>;	// "This is a restricted area!"
SOUND Trop08Snd, <trop08.wav>;	// death
SOUND Trop09Snd, <trop09.wav>;	// "X-ray Tango! Come in, over"
SOUND Trop10Snd, <trop10.wav>;	// "He's over there"
SOUND Trop11Snd, <trop11.wav>;	// "Son of a..."
SOUND Trop12Snd, <trop12.wav>;	// "Hey, scumbag!"

/////////////////////////////////////////////////////////////////
// Animated Textures
/////////////////////////////////////////////////////////////////
TEXTURE Trop0Tex {
        SCALE_XY        23,23;
        SIDES           4;
        CYCLES          4;
        BMAPS   Trop0001, Trop0101, Trop0201, Trop0101,
                Trop0002, Trop0102, Trop0202, Trop0102,
                Trop0003, Trop0103, Trop0203, Trop0103,
                Trop0002, Trop0102, Trop0202, Trop0102;
        DELAY   8, 8, 8, 8;
	  RANDOM  0.7;
        MIRROR  0,0,0,1;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE Trop0aTex {
        SCALE_XY        23,23;
        SIDES           4;
        CYCLES          3;
        BMAPS   Trop0001, Trop0101, Trop0201,
                Trop0002, Trop0102, Trop0202,
                Trop0003, Trop0103, Trop0203,
                Trop0002, Trop0102, Trop0202;
        DELAY   4, 4, 4;
        MIRROR  0,0,0,1;
	  RADIANCE 0.7;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE Trop1Tex {
        SCALE_XY        23,23;
        SIDES           8;
        CYCLES          4;
        BMAPS   Trop1001, Trop1101, Trop1201, Trop1301,
                Trop1002, Trop1102, Trop1202, Trop1302,
                Trop1003, Trop1103, Trop1203, Trop1303,
                Trop1004, Trop1104, Trop1204, Trop1304,
                Trop1005, Trop1105, Trop1205, Trop1305,
                Trop1204, Trop1304, Trop1004, Trop1104,
                Trop1203, Trop1303, Trop1003, Trop1103,
                Trop1202, Trop1302, Trop1002, Trop1102;
        DELAY   4, 5, 4, 5;
        MIRROR  0,0,0,0,0,1,1,1;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE Trop3Tex {
        SCALE_XY        23,23;
        SIDES           1;
        CYCLES          9;
        BMAPS   Trop3301, Trop3401,
                Trop3301, Trop3401, 
		    Trop3301, Trop3401,
                Trop3301, Trop3401, 
		    Trop3501;
	  SOUND   Trop04Snd;
	  SDIST   300;
	  SVOL    0.4;
	  SVDIST  25;
        DELAY   2, 2, 2, 2, 2, 2, 2, 3, 4;
	  SCYCLES 0, 1, 0, 0, 0, 0, 0, 0, 0;
	  AMBIENT 0.4;
	  FLAGS   CLIP;
}
TEXTURE Trop3aTex {
        SCALE_XY        23,23;
        SIDES           1;
        CYCLES          3;
        BMAPS   Trop3001, Trop3101, Trop3201;

        DELAY   4, 3, 3;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE Trop5Tex {
        SCALE_XY        23,23;
        SIDES           4;
        CYCLES          1;
        BMAPS   Trop5001, Trop5002, Trop5003, Trop5002;
	  MIRROR  0, 0, 0, 1;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE Trop5aTex {
        SCALE_XY        23,23;
        SIDES           4;
        CYCLES          1;
        BMAPS   Trop5001, Trop5002, Trop5003, Trop5002;
	  MIRROR  1, 1, 1, 0;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE Trop7Tex {
        SCALE_XY        39,39;
        SIDES           1;
        CYCLES          9;
        BMAPS   Trop7001, Trop7101, Trop7201, Trop7301,
                Trop7401, Trop7501, Trop7601, Trop7701,
                Trop7801;
        DELAY   5, 5, 5, 5, 5, 5, 5, 5, 5;
	  SOUND   Trop07Snd;
	  SDIST   300;
	  SVDIST  25;
	  SCYCLES 0, 1, 0, 0, 0, 0, 0, 0, 0;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE Trop7aTex {
        SCALE_XY        39,39;
        SIDES           1;
        CYCLES          9;
        BMAPS   Trop7001, Trop7101, Trop7201, Trop7301,
                Trop7401, Trop7501, Trop7601, Trop7701,
                Trop7801;
        DELAY   5, 5, 5, 5, 5, 5, 5, 5, 5;
	  MIRROR  1;
	  SOUND   Trop07Snd;
	  SDIST   300;
	  SVDIST  25;
	  SCYCLES 0, 1, 0, 0, 0, 0, 0, 0, 0;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE Trop8Tex {
        SCALE_XY        23,23;
        SIDES           1;
        CYCLES          8;
        BMAPS   Trop8001, Trop8101, Trop8201, Trop8301, 
	  	    Trop8401, Trop8501, Trop8601, Trop8701;
        DELAY   5, 4, 3, 3, 3, 3, 3, 3;
        SOUND   	Trop08Snd;
	  SCYCLES	0,1,0,0,0,0,0,0;
        SVOL	0.3;
        SDIST	200;
        SVDIST	25;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE Trop9Tex {
        SCALE_XY        23,23;
        SIDES           1;
        CYCLES          1;
        BMAPS   Trop9001;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}
TEXTURE TropATex {
        SCALE_XY        23,23;
        SIDES           8;
        CYCLES          4;
        BMAPS   TropA001, TropA101, TropA201, TropA301,
                TropA002, TropA102, TropA202, TropA302,
                TropA003, TropA103, TropA203, TropA303,
                TropA004, TropA104, TropA204, TropA304,
                TropA005, TropA105, TropA205, TropA305,
                TropA204, TropA304, TropA004, TropA104,
                TropA203, TropA303, TropA003, TropA103,
                TropA202, TropA302, TropA002, TropA102;
        DELAY   4, 5, 4, 5;
        MIRROR  0,0,0,0,0,1,1,1;
	  AMBIENT 0.2;
	  FLAGS   CLIP;
}

/////////////////////////////////////////////////////////////////
// Actions
/////////////////////////////////////////////////////////////////

ACTION TropTurn {
   RULE waitTime = 64 * RANDOM + 16;
   IF_BELOW RANDOM, 0.07; 
      BRANCH TropListen;
   IF_ABOVE RANDOM, 0.95;
      RULE rightTurnTrop = (rightTurnTrop-1)*(rightTurnTrop-1);
   IF_MAX rightTurnTrop;
      BRANCH TurnRight;
   BRANCH TurnLeft;
}

ACTION TropBackoff {
        SET MY.EACH_TICK,       NULL;
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
	  SET MY.EACH_CYCLE,	  CycleTropBack;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  1;

	  SET MY.SKILL4, 1;				// skill4 = state
        SET MY.TEXTURE, Trop1Tex;
        SET MY.SPEED, 0.3;
	  SET MY.TARGET, BULLET;
        CALL RepelAngle;
	  RULE MY.ANGLE = MY.ANGLE + RANDOM - 0.5;
	  CALL Probe;
	  WAITT 64;
	  CALL FollowAngle;
	  IF_ABOVE MY.SKILL1, 5;
		END;
	  BRANCH TropWait;
}

ACTION TropWait {
	  IF_NEQUAL MY.SKILL4, 4;
		GOTO cont;
	  IF_NEQUAL MY.VISIBLE, 1;
		GOTO cont;
	  END;
cont:
        SET MY.EACH_TICK,       NULL;
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
	  SET MY.EACH_CYCLE,	  CycleTropAttack;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  0;
	  SET MY.GROUND,		  0;
	  SET MY.HEIGHT,		  -0.05;

	  SET MY.SKILL4, 1;				// skill4 = state
        SET MY.TEXTURE, Trop0Tex;
        SET MY.SPEED, 0.0;
        SET MY.TARGET, NULL;
}
ACTION TropWander {
        SET MY.EACH_TICK,       NULL;
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
	  SET MY.EACH_CYCLE,	  CycleTropAttack;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  1;
	  SET MY.GROUND,		  0;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 1;				// skill4 = state
        SET MY.TEXTURE, Trop1Tex;
        SET MY.SPEED, 0.3;
        SET MY.TARGET, BULLET;
	  CALL LocateActor;				//////////
	  BRANCH Probe;
}
ACTION TropSearch {
        SET MY.EACH_TICK,       NULL;
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.EACH_CYCLE,      CycleTropAttack;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  1;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 11;				// skill4 = state
        SET MY.TEXTURE, TropATex;
        SET MY.SPEED, 0.3;
        SET MY.TARGET, BULLET;
	  CALL LocateActor;				//////////
	  BRANCH Probe;
}
ACTION TropHide {
        SET MY.EACH_TICK,       NULL;
        SET MY.IF_NEAR,         TropEscape;
        SET MY.IF_FAR,          NULL;
	  SET MY.EACH_CYCLE,	  TropHide;
        SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  1;
	  SET MY.BERKELEY,	  0;

	  IF_EQUAL	MY.SKILL4, 4;
		GOTO hidding;
	  SET MY.SKILL4, 4;				// skill4 = state
	  CALL RepelAngle;
        RULE MY.ANGLE = MY.ANGLE + (RANDOM-0.5) * 2.4;
        SET MY.TEXTURE, Trop1Tex;
	  SET MY.DIST, 	20;
        SET MY.SPEED, 0.6;
        SET MY.TARGET, BULLET;
	  CALL LocateActor;				//////////
	  CALL Probe;
	  WAITT 384;
	  IF_ABOVE MY.SKILL1, 5;
		END;
	  ADD MY.SKILL1, -2;
	  BRANCH TropFollowAttack;
hidding:
	  CALL RepelAngle;
        RULE MY.ANGLE = MY.ANGLE + (RANDOM-0.5) * 2.4;
        SET MY.TEXTURE, Trop1Tex;
	  SET MY.DIST, 	20;
        SET MY.SPEED, 0.6;
        SET MY.TARGET, BULLET;
	  CALL Probe;
}

ACTION TropListen {
	  IF_NEQUAL MY.SKILL4, 4;
		GOTO cont;
	  IF_NEQUAL MY.VISIBLE, 1;
		GOTO cont;
	  END;
cont:
	  IF_ABOVE MY.DISTANCE, 100;
		GOTO cont1;
	  IF_EQUAL MY.FLAG2, 0;
		GOTO cont1;
	  END;
cont1:
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.EACH_CYCLE,      CycleTropAttack;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  0;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 0;				// skill4 = state
        SET MY.TEXTURE, Trop0Tex;
        SET MY.SPEED, 0.0;
        SET MY.TARGET, NULL;
	  WAITT 120;
	  IF_BELOW MY.SKILL1, 9;
	  	BRANCH TropWander; 
}

ACTION TropWarning {
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.EACH_CYCLE,      TropBackoff;
	  SET MY.IF_HIT,		  TropHit;

	  SET MY.SKILL4, 7;				// skill4 = state
        SET MY.TEXTURE, Trop7Tex;
	  IF_BELOW RANDOM, 0.5;
        	SET MY.TEXTURE, Trop7aTex;
	  SET TROP_TEX, MY.TEXTURE;
	  RANDOMIZE randomTrop, 1;
	  IF_BELOW randomTrop, 0.2;
	  	GOTO overThere;
	  IF_BELOW randomTrop, 0.4;
		GOTO getDown;
	  SET TROP_TEX.SOUND, Trop07Snd;
	  GOTO cont;
getDown:
	  SET TROP_TEX.SOUND, Trop03Snd;
	  GOTO cont;
overThere:
	  SET TROP_TEX.SOUND, Trop02Snd;
	  GOTO cont;
cont:
        SET MY.SPEED, 0.0;
	  WAITT 32;
	  IF_ABOVE MY.SKILL1, 5;
		END;
	  RULE MY.ANGLE = MY.ANGLE + 2;
	  IF_BELOW RANDOM, 0.5;
	  	RULE MY.ANGLE = MY.ANGLE - 2;
	  BRANCH TropBackoff; 
}

ACTION TropLookFor {
	  IF_EQUAL MY.SKILL4, 10;
		END;
	  IF_ABOVE MY.DISTANCE, 100;
		END;
	  IF_EQUAL MY.FLAG2, 1;
		BRANCH TropFollowAttack;
	  IF_EQUAL MY.TARGET, FOLLOW;
		END;
	  IF_ABOVE MY.SKILL1, 5;
		END;
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.EACH_CYCLE,      TropSearch;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  0;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 10;				// skill4 = state
        SET MY.TEXTURE, TropATex;
        SET MY.SPEED, 0.0;

        SET MY.TARGET, BULLET;
	  IF_BELOW MY.DISTANCE, 100;
        	SET MY.EACH_CYCLE, TropFollowAttack;
}
ACTION TropFollow {
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.EACH_CYCLE,      CycleTropAttack;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  1;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 1;				// skill4 = state
        SET MY.TEXTURE, Trop1Tex;
        SET MY.SPEED, 0.4;
        CALL FollowAngle;
	  CALL LocateActor;				//////////
	  BRANCH Probe;
}
ACTION TropFollowAttack {
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.EACH_CYCLE,      CycleTropShoot;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  1;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 2;				// skill4 = state
        SET MY.TEXTURE, TropATex;
        SET MY.SPEED, 0.4;
        SET MY.TARGET, BULLET;
        CALL FollowAngle;
	  CALL LocateActor;				//////////
	  BRANCH Probe;
}
ACTION TropFollowWarning {
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.EACH_CYCLE,      CycleTropWarning;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  1;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 1;				// skill4 = state
        SET MY.TEXTURE, Trop1Tex;
        SET MY.SPEED, 0.4;
        SET MY.TARGET, BULLET;
	  CALL FollowAngle;
	  CALL LocateActor;				//////////
	  BRANCH Probe;
}
ACTION TropAim {
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.EACH_CYCLE,      TropShoot;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  0;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 3;				// skill4 = state
        SET MY.TEXTURE, Trop3aTex;
        SET MY.SPEED, 0.0;
	  CALL FollowAngle;
	  BRANCH TropShout;
}
ACTION TropShoot {
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.EACH_CYCLE,      CycleTropShoot;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  0;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 3;				// skill4 = state
        SET MY.TEXTURE, Trop3Tex;
        SET MY.SPEED, 0.0;
	  CALL FollowAngle;
        SET SHOOT_SECTOR, 2;
        SET SHOOT_FAC,    tropShootFactor;
        SET SHOOT_RANGE,  100;
	  SET SHOOT_Y,	  0.0;
        SHOOT MY;
	  IF_EQUAL HIT_DIST, 0;
		GOTO miss;
	  SET PLAYER_RESULT, RESULT;
	  BRANCH HitPlayerDelay;
miss:
	  BRANCH TropFollowAttack;
}
ACTION TropEscape {
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          TropHide;
        SET MY.EACH_CYCLE,      CycleTropHide;
	  SET MY.IF_HIT,		  TropHit;
	  SET MY.CAREFULLY,	  1;
	  SET MY.BERKELEY,	  0;

	  SET MY.SKILL4, 4;				// skill4 = state
        SET MY.TEXTURE, Trop1Tex;
	  SET MY.DIST, 	30;
        SET MY.SPEED, 0.4;
	  CALL RepelAngle;
	  RULE MY.ANGLE = MY.ANGLE + (RANDOM - 0.5)* 2;
        SET MY.TARGET, BULLET;
	  CALL LocateActor;				//////////
	  BRANCH Probe;
}
ACTION TropDead {
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.IF_HIT,          NULL;
        SET MY.EACH_CYCLE,      NULL;

	  SET MY.DIST,		  0;
	  SET MY.SKILL4, 9;				// skill4 = state
        SET MY.TEXTURE, Trop9Tex;
        SET MY.SPEED, 0.0;
        SET MY.TARGET, NULL;
        SET MY.PASSABLE, 1;
	  SET MY.CAREFULLY,	  0;
	  SET MY.BERKELEY,	  1;
	  IF_ABOVE RANDOM, 0.4;
		END;
	  IF_ABOVE RANDOM, 0.2;
		GOTO armour;
	  SET MY.IF_NEAR, PickTropAmmo;
	  SET MY.DIST,		  4;
	  END;
armour:
	  SET MY.IF_NEAR, PickTropArmour;
	  SET MY.DIST,		  4;
}
ACTION TropDie {
	  SET MY.SKILL1, 		  10;			// certify actor is dead
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.IF_HIT,          NULL;
        SET MY.EACH_CYCLE,      TropDead;

	  IF_NEQUAL MY.FLAG4, 0;
		CALL TropDropKey;
	  SET MY.SKILL4, 8;				// skill4 = state
        SET MY.TEXTURE, Trop8Tex;
        SET MY.SPEED, 0.0;
        SET MY.TARGET, NULL;
}
ACTION TropImplode {
	  RULE distX = (MY.X - EXPLOSION_CENTER.X)*(MY.X - EXPLOSION_CENTER.X)+
			   (MY.Y - EXPLOSION_CENTER.Y)*(MY.Y - EXPLOSION_CENTER.Y);
	  SQRT distX, distX;
	  RULE distZ = MY.HEIGHT - EXPLOSION_CENTER.HEIGHT;
	  IF_ABOVE distX, 10;
		BRANCH BeamReact;
        SET MY.PASSABLE, 1;
	  SET MY.CAREFULLY,	  0;
        SET MY.IF_NEAR,         NULL;
        SET MY.IF_FAR,          NULL;
        SET MY.IF_HIT,          NULL;
        SET MY.EACH_CYCLE,      VanishForGood;

	  SET MY.SKILL4, 8;				// skill4 = state
        SET MY.TEXTURE, Trop0aTex;
        SET MY.SPEED, 0.0;
        SET MY.TARGET, NULL;
	  IF_NEQUAL MY.FLAG4, 0;
		CALL TropDropKey;
}

ACTION TropHit { 
	  IF_EQUAL SHOOT_FAC, 0;
		    GOTO obstacle;
        IF_EQUAL HIT, MY;
                GOTO cont;
	  IF_EQUAL EXPLOSION_ON, 0;
		    GOTO obstacle;
	  RULE distX = (MY.X - EXPLOSION_CENTER.X)*(MY.X - EXPLOSION_CENTER.X)+
			   (MY.Y - EXPLOSION_CENTER.Y)*(MY.Y - EXPLOSION_CENTER.Y);
  	  SQRT distX, distX;
	  IF_ABOVE distX, 25;
		GOTO obstacle;
hit:
	  IF_NEQUAL EXPLOSION_ON, 2;
		    GOTO granade;
	  IF_EQUAL MY.VISIBLE, 1;
		    BRANCH TropImplode;
	  END;
granade:
	  RULE MY.SKILL1 = MY.SKILL1 + SHOOT_FAC * (20 - distX)/20;
	  GOTO exploded;
cont:
	  RULE MY.SKILL1 = MY.SKILL1 + SHOOT_FAC * (RANDOM+2)/3;
exploded:
        IF_ABOVE MY.SKILL1, 5;
                GOTO die;
        IF_BELOW RANDOM, 0.05;
                GOTO die;
        SET TROP_TEX, 	MY.TEXTURE;
        SET MY.IF_NEAR,	NULL;
        SET MY.IF_FAR,	NULL;
        SET MY.IF_HIT,	NULL;
	  IF_ABOVE RANDOM, 0.7;
		GOTO sonOfa;
        SET MY.TEXTURE,	Trop5Tex;
	  PLAY_SOUND	Trop05Snd, 0.2, MY;
	  GOTO wait;
sonofa:
        SET MY.TEXTURE,	Trop5aTex;
	  PLAY_SOUND	Trop11Snd, 0.2, MY;
wait:
        WAITT 4;
        SET MY.IF_NEAR,	TropEscape;
        SET MY.IF_FAR,	TropHide;
        SET MY.IF_HIT,	TropHit;
        SET MY.TEXTURE, TROP_TEX;
	  IF_ABOVE MY.SKILL1, 4;
        	BRANCH TropHide;
	  IF_NEQUAL MY.SKILL4, 4;
		BRANCH TropFollowAttack;
	  END;
die:
	  SET MY.SKILL1, 10;
        BRANCH TropDie;
        END;
obstacle:
        BRANCH TropTurn;
}
ACTION TropDropKey
{
	RULE Key2Tng.X = My.X;
	RULE Key2Tng.Y = My.Y;
	SET Key2Tng.GROUND,    1;		// ensures proper region for LOCATE
	RULE Key2Tng.HEIGHT = My.FLOOR_HGT-0.01;
	
	SET Key2Tng.INVISIBLE, 0;
	LOCATE Key2Tng;
	SET Key2Tng.GROUND,    0;
}

ACTION CycleTropAttack {
	CALL Probe;
	IF_NEQUAL MY.FLAG2, 0;		// flag 2 = player's got a gun
		GOTO attack;
	IF_EQUAL SHOT_SOUND_ON, 0;
		goto cont;
	CALL TropLookFor;
cont:
	CALL LookPlayer;
	IF_EQUAL MY.FLAG1, 0;
		END;
	IF_NEQUAL GUN_ON, 0;
		GOTO attack;
	IF_BELOW MY.DISTANCE, 20;
	  	BRANCH TropFollowWarning;
	END;
attack:
	SET MY.FLAG2, 1;			// flag 2 = player's got a gun
	CALL TropTalk;
  	IF_BELOW MY.DISTANCE, 200;
	BRANCH TropFollowAttack;
}
ACTION CycleTropHide {
	CALL RepelAngle;
	RULE MY.ANGLE = MY.ANGLE + (RANDOM - 0.5)* 2;
	CALL Probe;
	IF_EQUAL MY.VISIBLE, 1;
		END;
	CALL LookPlayer;
	IF_EQUAL MY.FLAG1, 0;
		GOTO stop;
	SET MY.SPEED, 0.6;
	SET MY.TARGET, BULLET;
	END;
stop:
	SET MY.SPEED, 0;
	SET MY.TARGET, NULL;
}

ACTION CycleTropBack {
	  CALL Probe;
}

ACTION CycleTropShoot {
	CALL Probe;
	IF_ABOVE MY.SKILL1, 9;
		BRANCH TropDie;
	IF_ABOVE MY.DISTANCE, 300;
		BRANCH TropWait;
	IF_ABOVE MY.DISTANCE, 100;
		GOTO attack;
	CALL FollowAngle;
	CALL LookPlayer;
	IF_EQUAL MY.FLAG1, 0;
		GOTO attack;
shoot:
        SET SHOT_SOUND_ON, 1;
        SET shotSecCount,  0;
	  IF_ABOVE RANDOM, 0.8;
        	BRANCH  TropAim;
        BRANCH  TropShoot;
attack:
        BRANCH  TropFollowAttack;
}

ACTION CycleTropWarning {
	  CALL Probe;
        IF_NEQUAL GUN_ON, 0;
        	BRANCH TropFollowAttack;
	  IF_ABOVE MY.DISTANCE, 10;
		GOTO cont;
        SET MY.EACH_CYCLE,      NULL;
	  BRANCH TropWarning;
cont:
	  IF_ABOVE MY.DISTANCE, 40;
		BRANCH TropWait;
	  CALL FollowAngle;
}

ACTION TropTalk {
	RANDOMIZE randomTrop, 1;
	IF_ABOVE randomTrop, 0.9;
		GOTO snd1;
	IF_ABOVE randomTrop, 0.8;
		GOTO snd2;
	END;
snd1:
	PLAY_SOUND Trop09Snd, 0.3, MY;
	END;
snd2:
	PLAY_SOUND Trop01Snd, 0.3, MY;
}

ACTION TropShout {
	RANDOMIZE randomTrop, 1;
	IF_ABOVE randomTrop, 0.8;
		GOTO snd1;
	IF_ABOVE randomTrop, 0.4;
		GOTO snd2;
	IF_ABOVE randomTrop, 0.1;
		GOTO snd3;
	PLAY_SOUND Trop06Snd, 0.3, MY;
	END;
snd1:
	PLAY_SOUND Trop12Snd, 0.4, MY;
	END;
snd2:
	PLAY_SOUND Trop10Snd, 0.3, MY;
	END;
snd3:
	PLAY_SOUND Trop03Snd, 0.3, MY;
}

ACTION PickTropAmmo {
        ADD AMMO_MP5, 30;
	  CALL ShowAmmo;
        SET MY.IF_NEAR, NULL;
        PLAY_SOUND Ammo01Snd, 0.3;
        SET     MESSAGE_TEXT, Get01Str;
        BRANCH  DisplayMessage;
}
ACTION PickTropArmour {
	  IF_ABOVE PLAYER_ARMOUR, 190;
		END;
	  ADD PLAYER_ARMOUR, 20;
        SET MY.IF_NEAR, NULL;
        PLAY_SOUND Armo01Snd, .5;
        SET   MESSAGE_TEXT, Get10Str;
        CALL  DisplayMessage;
}
/////////////////////////////////////////////////////////////////
// Actor
/////////////////////////////////////////////////////////////////

ACTOR TropAct {
      TEXTURE 	Trop1Tex;
      HEIGHT 	-0.05;
	DIST		20;
      SPEED 	0.3;
	FLAGS		CAREFULLY,FRAGILE;
	IF_HIT	TropHit;
	EACH_TICK	TropWait;
}
